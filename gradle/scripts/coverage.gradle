apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.palantir.jacoco-full-report'
apply plugin: 'jacoco'

// Must match the version used in jacoco4sbt plugin.
ext.jacocoVersion = '0.7.1.201405082137'

jacoco {
  toolVersion = jacocoVersion
}

jacocoFull {
    excludeProject ":backend-service", ":hadoop-dataset-extractor-standalone", ':data-model', ':metadata-etl', ':wherehows-common'
}

// Add the jacoco plugin to each project
subprojects {
  plugins.apply('java')
  plugins.apply('jacoco')

  jacoco {
    toolVersion = jacocoVersion
  }

  jacocoTestReport {
    reports {
      xml.enabled true
      html.enabled true
      csv.enabled false
    }
  }
}

// Configure the coveralls task to read the aggregated JaCoCo report
coveralls {
  jacocoReportPath = "${buildDir}/reports/jacoco/jacocoFullReport/jacocoFullReport.xml"
}

tasks.coveralls {
  doFirst {
    // Must set this at task execution time for the play.gradle's sourceSets modification to take effect.
    coveralls.sourceDirs = subprojects.sourceSets.main.java.srcDirs.flatten()
  }
}
