apply plugin: 'java'
apply plugin: 'org.hidetake.swagger.generator'


dependencies {

  implementation project(':metadata-models')
  implementation project(':metadata-service:restli-client')
  implementation project(':metadata-service:auth-impl')
  implementation project(':metadata-service:factories')
  implementation project(':metadata-service:services')
  implementation project(':metadata-integration:java:openlineage-converter')
  implementation project(':metadata-integration:java:datahub-event')

  implementation "io.openlineage:openlineage-java:$openLineageVersion"
  implementation externalDependency.springBoot
  implementation externalDependency.springCore
  implementation externalDependency.springDocUI
  implementation externalDependency.springWeb
  implementation externalDependency.springWebMVC
  implementation externalDependency.springBeans
  implementation externalDependency.springContext

  implementation externalDependency.reflections
  implementation externalDependency.slf4jApi
  compileOnly externalDependency.lombok

  implementation externalDependency.antlr4Runtime
  implementation externalDependency.antlr4
  implementation "javax.servlet:javax.servlet-api:4.0.1"

  annotationProcessor externalDependency.lombok

  testImplementation externalDependency.testng
  testImplementation externalDependency.mockito
  testImplementation externalDependency.springBootTest

  swaggerCodegen externalDependency.swaggerCli
}

sourceSets {
  main {
    java {
      srcDirs = ["$buildDir/openapi/generated/src/main/java", 'src/main/java']
    }
  }
}


// https://github.com/int128/gradle-swagger-generator-plugin#task-type-generateswaggercode
task openApiGenerate(type: GenerateSwaggerCode) {
  inputFile = file("$projectDir/src/main/resources/openlineage.json")
  outputDir = file("$buildDir/openapi/generated")
  language = "spring"
  components = ["apis"]
  templateDir = file("$projectDir/src/main/resources/JavaSpring")
  additionalProperties = [
          'group-id'           : "io.datahubproject",
          'dateLibrary'        : "java8",
          'java11'             : "true",
          'interfaceOnly'      : 'true',
          'modelPropertyNaming': "original",
          'modelPackage'       : "io.datahubproject.openlineage.model",
          'apiPackage'         : "io.datahubproject.openlineage.generated.controller",
          'delegatePattern'    : "true"
  ]
}
tasks.getByName("compileJava").dependsOn(openApiGenerate)

//checkstyleMain.exclude '**/generated/**'